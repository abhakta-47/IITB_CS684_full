(*********************************************************************************
Inputs:
    Values from 5 White Line Sensors.
    Each Sensor value ranges between: 0 to 1023.
    On white surface, sensor values are low and for black surface, sensor values are high.
Outputs:
    3 for controlling the motion of the Robot (1 for Direction and 2 for Motor's velocities).
    From the final heptagon node, 3 outputs are required: 2 velocity values (of left and right motors) and direction.
    Directions list:
        Direction	Representation
        Stop	        0
        Forward	        1
        Left	        2
        Right	        3
        Backward	    4
        Brake   	    9
    Motor velocity ranges between 0 to 100. It should not be negative or greater than 100.
- Consider a black line on white surface.
- Use PID controller for line following.
- You are free to make assumptions regarding
    readings of white line sensors at different instances,
    distance covered by robot for different motions in single step etc.
**********************************************************************************)

(**********************************
CONFIGURATION CONSTANTS START
***********************************)
(* Sensor related *)
const sensor_weights: int^5 = [-5, -2, 0, 2, 5]
(* PID gains and ranges *)
(* Gains should be determined through field trials *)
(* for float approximation kscale is used effective k* would be k*/kscale *)
const kscale_white: int = 100  -- Using integer division for scaling down
const kscale_black: int = -120  -- Using integer division for scaling down
const kp: int = 2 -- effective kp/kscale
const kd: int = 5 -- effective kd/kscale
const ki: int = 0 -- effective ki/kscale
const max_i: int = 200000000  -- to prevent integral overflow
(* speeds *)
const base_speed: int = 40
const safe_speed_both: int = 40
const safe_speed_high: int = 50
const safe_speed_low: int = 10
const safe_speed_park: int = 50
(*  *)
const low_thresh: int = 699 -- sensor value less than low_thresh guarantees white
const high_thresh: int = 700 -- sensor value more than high_thresh guarantees black
const higher_thresh: int = 800 -- sensor value more than high_thresh guarantees black
const obstacle_ahead_thresh: int = 3
(********************************
CONFIGURATION CONSTANTS END
*********************************)

(* PID error computation *)
node calPidError(value, scale_down: int) returns (pid_error: int)
var p, i, d: int;
let
     p = value;
     (* Integral term with overflow prevention *)
     i = if ((0->pre(i) + value) <= max_i) then (0->pre(i) + value) else max_i;
     d = value - 0->pre(value);
     pid_error = (kp*p + ki*i + kd*d) / scale_down;
tel

(* Computes weighted sum of sensor values *)
node weightedSum(sen_value, sen_weight, prev_sum: int) returns (weighted_sum: int)
let
    weighted_sum = prev_sum + sen_value * sen_weight;
tel

(* Computes weighted avg of sensor values *)
node senWeightedAvg(sen: int^5) returns (sensor_avg: int)
var aritmetic_sum: int;
let
    aritmetic_sum = fold<<5>>(+)(sen, 0) - sen[2];
    sensor_avg = if aritmetic_sum = 0 then 0 else fold<<5>>weightedSum(sen, sensor_weights, 0);
tel

(*  esure motor speed stays in range [0,100] *)
node safe_motor_update(cur_speed, change: int) returns (new_speed: int)
let
    new_speed =     if (cur_speed + change) > 100 then 100
                    else if (cur_speed + change) < 0 then 0
                    else (cur_speed + change);
tel

node normalize_value(min_val, max_val, value: int) returns (norm_val: int)
var safe_val: int;
let
    safe_val = if value >= max_val then max_val else if value <= min_val then min_val else value;
    norm_val = ((safe_val - min_val)*1000)/(max_val - min_val)
tel 

node updateMins(cur_val, cur_thresh, cur_min: int) returns(new_min: int)
let
    new_min = if (cur_val>cur_min) and (cur_val<cur_thresh) then cur_val else cur_min;
tel

node updateMaxs(cur_val, cur_thresh, cur_max: int) returns(new_max: int)
let
    new_max = if (cur_val<cur_max) and (cur_val>cur_thresh) then cur_val else cur_max;
tel

node max(val_a, val_b: int) returns (val_m: int)
let
    val_m = if val_a >= val_b then val_a else val_b;
tel

node abs(input: int) returns(out: int)
let
    out = if input>=0 then input else -1*input;
tel

node pid_err_to_speeds_park(pid_error: int) returns(v_l, v_r, dir: int)
var sharp_thresh: int;
let
    sharp_thresh = 40;
    v_l = if abs(pid_error) <= sharp_thresh then safe_motor_update(safe_speed_park, pid_error/2) else 100;
    v_r = if abs(pid_error) <= sharp_thresh then safe_motor_update(safe_speed_park, -1*pid_error/2) else 100;
    dir = 1;
tel

(* Main function for line following *)
node main(sen0, sen1, sen2, sen3, sen4, ir_value: int; obs_left, obs_right: bool) returns ( v_l, v_r, dir: int;)
var raw_sen: int^5; sensor_avg: int; all_high: bool;
(* last : int = 1;  *)
last pid_error: int = 0;
last last_error: int = 0;
last max_vals: int^5 = [1023, 1023, 1023, 1023, 1023];
last min_vals: int^5 = [0, 0, 0, 0, 0];
last thresh_vals: int^5 = [500, 500, 500, 700, 500];
last sen: int^5 = [0, 0, 0, 0, 0];
last inx_counter: int = 0;
last parking_cycles_thresh: int = 0;
let
    raw_sen = [sen0, sen1, sen2, sen3, sen4];
    sen = map<<5>>normalize_value((last min_vals), (last max_vals), raw_sen);
    (* processed_sen = sen; *)
    -- Compute error and PID error
    sensor_avg = senWeightedAvg(sen);

    -- Determine robot movement direction
    all_high = (sen[0]>=high_thresh and sen[1]>=high_thresh and sen[2]>=high_thresh and sen[3]>=high_thresh and sen[4]>=high_thresh);

    automaton
        state Calibrate
            do v_l=safe_speed_both; v_r=safe_speed_both; dir=2;
                min_vals = map<<5>>updateMins(raw_sen, thresh_vals, (last min_vals));
                max_vals = map<<5>>updateMaxs(raw_sen, thresh_vals, (last max_vals));
                thresh_vals = (last thresh_vals);
        state Idle
            do 
            v_l=0; v_r=0; dir=9;
            unless all_high then Start
        state Start
            do 
            v_l=base_speed; v_r=base_speed; dir=1;
            until not all_high then WonB
        state WonB
            var sharp_thresh: int;
            do
                sharp_thresh = 15;
                automaton
                    state PID
                        do 
                            pid_error = calPidError(sensor_avg, kscale_white);
                            last_error = if pid_error<=sharp_thresh then (last last_error) else if pid_error>0 then 1 else -1;
                            v_l = if pid_error<=sharp_thresh then safe_motor_update(base_speed, pid_error/2) else safe_motor_update(base_speed, sharp_thresh/2);
                            v_r = if pid_error<=sharp_thresh then safe_motor_update(base_speed, -1*pid_error/2) else safe_motor_update(base_speed, -1*sharp_thresh/2);
                            dir = 1;
                        unless (sen[0]<=low_thresh and sen[1]<=low_thresh and sen[2]<=low_thresh and sen[3]<=low_thresh and sen[4]<=low_thresh) then Sharp
                    state Sharp
                        do 
                            v_l = safe_speed_both;
                            v_r = safe_speed_both;
                            dir = if last_error=0 then 4 else if last_error > 0 then 3 else 2;
                        until (sen[0]<=low_thresh and sen[1]<=low_thresh and sen[2]>=high_thresh and sen[3]<=low_thresh and sen[4]<=low_thresh) then PID
                end
            unless (sen[0]>=high_thresh and (sen[1]<=low_thresh or sen[2]<=low_thresh or sen[3]<=low_thresh) and sen[4]>=high_thresh) then BonW
        state BonW
            var inx_condition: bool; sharp_thresh: int;
            do 
                sharp_thresh = 30;
                pid_error = calPidError(sensor_avg, kscale_black);
                v_l = if abs(pid_error) <= sharp_thresh then safe_motor_update(base_speed, pid_error/2) else safe_motor_update(0, abs(pid_error)+20);
                v_r = if abs(pid_error) <= sharp_thresh then safe_motor_update(base_speed, -1*pid_error/2) else safe_motor_update(0, abs(pid_error)+20);
                dir = if abs(pid_error) <= sharp_thresh then 1 else if pid_error > 0 then 3 else 2;
                last_error = if pid_error=0 then (last last_error) else if pid_error>0 then 1 else -1;
                inx_condition = (sen[0]<=low_thresh and sen[1]<=low_thresh and sen[2]<=low_thresh) or (sen[3]<=low_thresh and sen[1]<=low_thresh and sen[2]<=low_thresh) or (sen[4]<=low_thresh and sen[3]<=low_thresh and sen[2]<=low_thresh);
            until inx_condition then Intersection
            unless not (ir_value=0) then ObstacleAvoid
        state Intersection
            var complete: bool;
            do
                automaton
                    state Counter
                        do
                            dir=0; v_l=0; v_r=0;
                            inx_counter = if (last inx_counter) >= 2 then (last inx_counter) + 1 else (last inx_counter);
                            complete = false;
                        until true then Entry
                    state Entry
                        var only_one_low: bool;
                        do
                            dir = 1;
                            v_l = base_speed;
                            v_r = base_speed;
                            complete = false;
                            only_one_low = (sen[0] <= low_thresh and sen[1] >= high_thresh and sen[2] >= high_thresh and sen[3] >= high_thresh and sen[4] >= high_thresh) or (sen[1] <= low_thresh and sen[0] >= high_thresh and sen[2] >= high_thresh and sen[3] >= high_thresh and sen[4] >= high_thresh) or (sen[2] <= low_thresh and sen[0] >= high_thresh and sen[1] >= high_thresh and sen[3] >= high_thresh and sen[4] >= high_thresh) or (sen[3] <= low_thresh and sen[0] >= high_thresh and sen[1] >= high_thresh and sen[2] >= high_thresh and sen[4] >= high_thresh) or (sen[4] <= low_thresh and sen[0] >= high_thresh and sen[1] >= high_thresh and sen[2] >= high_thresh and sen[3] >= high_thresh);
                        until (inx_counter<=2) and all_high then ExitRight
                            | (inx_counter=3 or inx_counter=4) and only_one_low then ExitLeft
                            | (inx_counter=5) and only_one_low then ExitStraight
                    state ExitRight
                        do    
                            dir = 3;
                            v_l = safe_speed_both;
                            v_r = safe_speed_both;
                            complete = not all_high;
                    state ExitLeft
                        do    
                            dir = 2;
                            v_l = safe_speed_both;
                            v_r = safe_speed_both;
                            complete = (sen[0]<=low_thresh);
                    state ExitStraight
                        do
                            dir=9; v_l=0; v_r=0;
                            complete = true;
                end
            until complete and not (inx_counter=5) then BonW
                | complete and inx_counter=5 then Parking
            unless not (ir_value=0) then ObstacleAvoid
        state ObstacleAvoid
            var complete, all_very_high: bool;
            do
                all_very_high = (sen[0]>=higher_thresh and sen[1]>=higher_thresh and sen[2]>=higher_thresh and sen[3]>=higher_thresh and sen[4]>=higher_thresh);
                automaton
                    state Wait
                        var ncycles: int;
                        do
                            v_l = 0;
                            v_r = 0;
                            dir = 9;
                            ncycles = 0 -> pre(ncycles) + 1;
                            complete = if ir_value=0 then true else false;
                        until ncycles>1000 then FullRight
                    state FullRight
                        do
                            v_l = safe_speed_both;
                            v_r = safe_speed_both;
                            dir = 3;
                            complete = false;
                        unless obs_left then SlightStraight
                    state SlightStraight
                        var ncycles: int;
                        do 
                            dir=1; v_l=safe_speed_both; v_r=safe_speed_both;
                            ncycles = 0->pre(ncycles)+1;
                            complete = false;
                        until all_high then SlightLeft 
                    state SlightRight
                        do
                            dir = 1;
                            v_l=safe_speed_high; v_r=safe_speed_low;
                            complete = false;
                        unless not obs_left then SlightLeft
                            | not (ir_value=0) then FullRight
                            | (not all_high) then ExitStart
                    state SlightLeft
                        do
                            dir = 1;
                            v_l=safe_speed_low; v_r=safe_speed_high;
                            complete = false;
                        unless obs_left then SlightRight
                            | not (ir_value=0) then FullRight
                            | (not all_high) then ExitStart
                    state ExitStart
                        do
                            dir=9;
                            v_l=0;
                            v_r=0;
                            complete = false;
                            inx_counter = 2;
                        unless (sen[0]<=low_thresh or sen[1]<=low_thresh) then ExitEnd1
                            | (sen[2]<=low_thresh or sen[3]<=low_thresh or sen[4]<=low_thresh) then ExitEnd2
                    state ExitEnd1
                        do
                            dir=1;
                            v_l=safe_speed_high;
                            v_r=safe_speed_low;
                            automaton
                                state St_high_0
                                    do complete = false;
                                    unless sen[0]<=low_thresh then St_low_1
                                state St_low_1
                                    do complete = false;
                                    unless sen[0]>=high_thresh then St_high_1
                                state St_high_1
                                    do complete = sen[0]<=low_thresh;
                            end
                    state ExitEnd2
                        var sen_1_low1, sen_1_high1: bool;
                        do
                            dir=1;
                            v_l=safe_speed_high;
                            v_r=safe_speed_low;
                            sen_1_low1 = false -> if pre(sen_1_low1) then true else sen[1]<=low_thresh;
                            sen_1_high1 = false -> if pre(sen_1_high1) then true else  sen_1_low1 and sen[2]>=high_thresh;
                            complete = sen_1_high1 and sen[2]<=low_thresh;
                end
            until complete then BonW
        state Parking
            do
                automaton
                    state Train
                        var cycles_left, cycles_right: int; left_was_high, right_was_high: bool;
                        do
                            pid_error = calPidError(sensor_avg, kscale_black);
                            (v_l, v_r, dir) = pid_err_to_speeds_park(pid_error);

                            left_was_high = false -> if (pre left_was_high) then true else obs_left;
                            right_was_high = false -> if (pre right_was_high) then true else obs_right;
                            cycles_left  = 0 -> if not obs_left and left_was_high then pre(cycles_left)  +1 else 0;
                            cycles_right = 0 -> if not obs_right and  right_was_high then pre(cycles_right) +1 else 0;
                            parking_cycles_thresh =  if obs_left and obs_right then last parking_cycles_thresh else 
                                             if not obs_left then max((last parking_cycles_thresh), cycles_left) else
                                             if not obs_right then max((last parking_cycles_thresh), cycles_right) 
                                             else max((last parking_cycles_thresh), max(cycles_left, cycles_right));
                        until parking_cycles_thresh>0 and obs_left and obs_right then FindSpace
                    state FindSpace
                        var cycles_left, cycles_right: int;
                        do
                            cycles_left  = 0 -> if obs_left  then 0 else pre(cycles_left)  +1;
                            cycles_right = 0 -> if obs_right then 0 else pre(cycles_right) +1;
                            pid_error = calPidError(sensor_avg, kscale_black);
                            (v_l, v_r, dir) = pid_err_to_speeds_park(pid_error);
                        until cycles_left  > parking_cycles_thresh*7/3 then GoBackL
                            | cycles_right > parking_cycles_thresh*7/3 then GoBackR
                    state GoBackL
                        var locdir, ncycles: int;
                        do
                            ncycles = 0->pre(ncycles) +1;
                            pid_error = calPidError(sensor_avg, kscale_black);
                            (v_l, v_r, locdir) = pid_err_to_speeds_park(pid_error);
                            dir=4;
                        until ncycles > parking_cycles_thresh*7/6 then ParkLeft
                    state GoBackR
                        var locdir, ncycles: int;
                        do
                            ncycles = 0->pre(ncycles) +1;
                            pid_error = calPidError(sensor_avg, kscale_black);
                            (v_l, v_r, locdir) = pid_err_to_speeds_park(pid_error);
                            dir=4;
                        until ncycles > parking_cycles_thresh*7/6 then ParkRight
                    state ParkLeft
                        var ncycles: int;
                        do
                            ncycles = 0->pre(ncycles) +1;
                            v_l=safe_speed_park; v_r=safe_speed_park; dir=3;
                        until ncycles > parking_cycles_thresh*3/4 then GoReverse
                    state ParkRight
                        var ncycles: int;
                        do
                            ncycles = 0->pre(ncycles) +1;
                            v_l=safe_speed_park; v_r=safe_speed_park; dir=2;
                        until ncycles > parking_cycles_thresh*3/4 then GoReverse
                    state GoReverse
                        var ncycles: int;
                        do
                            ncycles = 0->pre(ncycles) +1;
                            v_l=safe_speed_park; v_r=safe_speed_park; dir=4;
                        until ncycles > parking_cycles_thresh*3/3 then Stop
                    state Stop
                        do
                            v_l=0; v_r=0; dir=9;                        
                end
    end
tel
